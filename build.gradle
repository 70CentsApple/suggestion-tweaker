plugins {
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id 'dev.architectury.loom' version '1.6.397' apply false
	id "me.shedaniel.unified-publishing" version "0.1.+" apply false
	id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'com.github.johnrengelman.shadow'

	repositories {
		maven { // vineflower
			name = "sonatype-oss-snapshots1"
			url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
		}
	}

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		vineflowerDecompilerClasspath "org.vineflower:vineflower:1.11.0-SNAPSHOT" artifact {
			url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/org/vineflower/vineflower/1.11.0-SNAPSHOT/vineflower-1.11.0-20240501.032551-24.jar'
		}

		minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
		// necessary to patch "unfixable conflicts" error; check if fixed better in the future
		mappings loom.layered() {
			it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
			it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${project.yarn_mappings_patch_version}")
		}
	}
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'

	archivesBaseName = rootProject.archives_base_name
	version = rootProject.minecraft_version+'-'+rootProject.mod_version
	group = rootProject.maven_group

	tasks.withType(JavaCompile) {
		// ensure that the encoding is set to UTF-8, no matter what the system default is
		// this fixes some edge cases with special characters not displaying correctly
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
		// If Javadoc is generated, this must be specified in that task too.
		options.encoding = "UTF-8"

		// Minecraft 1.20.5 upwards uses Java 21.
		options.release = 21
	}

	java {
		withSourcesJar()
	}
}

task modPublish
